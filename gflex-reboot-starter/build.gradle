apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'org.apache.flex.blazeds', name: 'blazeds-spring-boot-starter', version: "${blazedsVersion}"
    compile(group: 'org.grails', name: 'gorm-hibernate5-spring-boot', version: "${gormVersion}") {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    compile group: 'com.h2database', name: 'h2', version: '1.4.195'
    //compile 'com.alibaba:druid-spring-boot-starter:1.1.2'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.16'
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile 'com.google.guava:guava:23.0'
    // Use the awesome Spock testing and specification framework
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testCompile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

springBoot {
    mainClass = 'ns.gflex.GflexBootApplication'
}

bootRun {
    systemProperties System.properties
}

test {
    systemProperties System.properties
    systemProperty 'spring.profiles.active', 'dev'
    //systemProperty 'init', 'true'
}

group = 'neo.script.gflex'
version = '1.0'

task sourceJar(type: Jar) {
    from(sourceSets.main.allSource, sourceSets.test.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'gflex-reboot-starter'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/neoscript99/gflex-reboot/issues'
        websiteUrl = 'https://github.com/neoscript99/gflex-reboot.git'
        vcsUrl = 'https://github.com/neoscript99/gflex-reboot'
        publicDownloadNumbers = true

        version {
            name = project.version
            desc = "gflex-reboot-starter $project.version"
            released = new Date()
            vcsTag = "v$project.version"
        }
    }
}
